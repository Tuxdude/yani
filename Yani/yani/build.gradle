apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    File releasePropertiesFile = project.file('release.properties')
    if (releasePropertiesFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(releasePropertiesFile))
        if (new File(props["release-signing-config"]).exists()) {
            props.load(new FileInputStream(file(props["release-signing-config"])))

            signingConfigs {
                release {
                    storeFile file(props['keystore'])
                    storePassword props['keystore.password']
                    keyAlias props['keyAlias']
                    keyPassword props['keyPassword']
                }
            }
            buildTypes {
                release {
                    signingConfig signingConfigs.release
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:20.+'
    compile 'org.tuxdude:pagerslidingtabstrip:1.0.1.c'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'
}

repositories {
    maven {
        url 'https://raw.githubusercontent.com/Tuxdude/maven-artifacts/master'
    }

    File localRepoFile = project.file('local.repo.properties')
    if (localRepoFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(localRepoFile))
        maven {
            url props['localRepoUri']
        }
    }

    mavenLocal()
}
